PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ufo: <http://onto.fel.cvut.cz/ontologies/ufo/>
PREFIX gr: <http://onto.fel.cvut.cz/ontologies/graph#>

CONSTRUCT{
    ?el a owl:Class;
        rdfs:label ?label.

    ufo:has-stereotype a owl:AnnotationProperty.
    ?stereotype a owl:Class.
    ?el ufo:has-stereotype ?stereotype.
} WHERE {
    ?el gr:label ?label;
        a gr:node.

    ?el gr:fillColor ?color. # colorless nodes are not used as classes, they are used as refereces or cardinalities.
    OPTIONAL{
        ?el gr:stereotype ?s.
        BIND(iri(concat(str(ufo:), ?s)) as ?stereotype)
    }
}