PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX cm: <http://onto.fel.cvut.cz/ontologies/csat-maintenance/>
PREFIX  rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX ofn: <http://www.ontotext.com/sparql/functions/>
PREFIX spif: <http://spinrdf.org/spif#>
PREFIX ufo: <http://onto.fel.cvut.cz/ontologies/ufo/>

SELECT ?wp ?wpId ?tt ?referencedTask ?issueTime ?endTime ?estMin ?tType ?averageTime ?findingAverageTime ?type ?taskcat
WHERE
{
    ?wp a cm:workpackage.
    ?wp cm:id ?wpId.

    # tasks
    ?tt cm:is-part-of-workpackage ?wp.
    OPTIONAL{
        ?tt cm:time-estimate-in-hours ?estMin .
    }
    OPTIONAL{
        ?tt cm:issue-time ?issueTime.
    }
    OPTIONAL{
        ?tt cm:end-time ?endTime.
    }
    OPTIONAL {
        ?tt cm:references-task ?referencedTask
    }

    # task type
    ?tt a ?tType.
    OPTIONAL{
        ?tType a cm:task-type .
        BIND(true as ?_tType)
    }

    OPTIONAL{
        ?tType a owl:Class;
                cm:id ?type;
                rdfs:subClassOf ?cls.
        BIND(replace(str(?cls), "^.*/([^/]+)$", "$1") as ?taskcat)
    }

    FILTER(strstarts(str(?tType), str(cm:)) && !(?tType in (cm:task-card, cm:maintenance-work-order, cm:scheduled-work-order, cm:complex-execution)))
    FILTER(bound(?_tType) || bound(?type))
    OPTIONAL {
        ?tType cm:time-estimate-in-hours ?averageTime .
    }
    OPTIONAL {
        ?tt a ?componentFailureClass.
        ?componentFailureClass rdfs:subClassOf cm:component-failure--task .
        ?componentFailureClass cm:avg-duration ?findingAverageTime .
    }
}