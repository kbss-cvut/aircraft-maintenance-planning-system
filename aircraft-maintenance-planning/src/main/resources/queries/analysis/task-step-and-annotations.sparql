PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX cm: <http://onto.fel.cvut.cz/ontologies/csat-maintenance/>
PREFIX rdf:  <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX ofn: <http://www.ontotext.com/sparql/functions/>
PREFIX spif: <http://spinrdf.org/spif#>
PREFIX ufo: <http://onto.fel.cvut.cz/ontologies/ufo/>
PREFIX csat-text: <http://onto.fel.cvut.cz/ontologies/csat/enhance-wo-text-0.1/>
PREFIX csvw: <http://www.w3.org/ns/csvw#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

PREFIX termit: <http://onto.fel.cvut.cz/ontologies/application/termit/pojem/>

SELECT ?wp ?task ?step ?stepIndex ?workOrderText ?workOrderActionText ?timeEstimateInHours ?annotatedText
?componentUri ?componentLabel ?failureUri ?failureLabel ?aggregateScore
WHERE
{
    ?wp a cm:workpackage .
    ?wp cm:id ?wpId .

    # tasks
    ?task cm:is-part-of-workpackage ?wp .
    ?task cm:has-work-order-step ?step .

    ?step cm:work-order-text ?workOrderText .
    OPTIONAL{?step cm:step-index ?stepIndex .}
    OPTIONAL{?step cm:work-order-action-text ?workOrderActionText . }
#        OPTIONAL{?step cm:time-estimate-in-hours ?timeEstimateInHours . }

    ?step a ?stepType .
    ?stepType rdfs:subClassOf cm:work-order-step .

    OPTIONAL{
        ?step cm:described-finding ?finding .
        ?finding cm:has-failure-occurrence ?failureOccurrence .
        OPTIONAL{?finding csat-text:WO_text ?annotatedText}
        ?failureOccurrence termit:je-přiřazením-relace ?failureInstance.

        OPTIONAL{
            ?failureInstance csat-text:has-argument1 ?componentUri .
            OPTIONAL{?componentUri skos:prefLabel ?componentLabel .}
        }
        OPTIONAL{
            ?failureInstance csat-text:has-argument2 ?failureUri .
            OPTIONAL{?failureUri skos:prefLabel ?failureLabel .}
        }
        OPTIONAL{?failureOccurrence termit:má-skóre ?aggregateScore.}
    }
}